//3/18/2013
//Script written in Collect Edition of Sauerbraten a.k.a. Cube 2
//SomeDude
//Contact:  stevenquack86vg@gmail.com


editbind F5 [if (cleargui 1) [] [obrmodels]]


createine = [if (=s (getalias $arg1) "") [$arg1 = $arg2]]

getfiletype = [
    _gfti = (strlen $arg1)
    while [(&& $_gfti (! (=s (substr $arg1 (- $_gfti 1) 1) ".")))] [ _gfti = (- $_gfti 1)]
    if (> $_gfti 0) [result (substr $arg1 $_gfti)] [result "folder"]
]

_prevdir = [
    i = (- (strlen $arg1) 1)
    while [(&& $i (! (=s (substr $arg1 (- $i 1) 1) "\")))] [i = (- $i 1)]
    result (substr $arg1 0 (- $i 1))
]

//starting at: "packages\brush\"
obrguidir = ""
createine obrscreenshot 1

obrmodels = [
    if ($numargs) [obrguidir = $arg1]
    guirolloveraction = []
    newobrname = ""
    obrlist = ""
    obrfolderlist = ""
    local curobr
    loopfiles curobr (concatword "packages\brush\" $obrguidir) "" [  //Loops through ALL files
        cases (getfiletype $curobr) "obr" [obrlist = (concat $obrlist (concatword "^"" (substr $curobr 0 (- (strlen $curobr) 4)) "^""))] "folder" [obrfolderlist = (concat $obrfolderlist (concatword "^"" $curobr "^""))]
    ]
    if (&& (=s $obrlist "") (=s $obrfolderlist "")) [sleep 1 [showgui no_obrmodels]] [sleep 1 [showgui obrmodels]]
]

newgui no_obrmodels [
    if (! (=s $obrguidir "")) [guibutton "^f2back" [obrguidir = (_prevdir $obrguidir); sleep 1 [obrmodels]] "arrow_bw"]
        if ($havesel) [
            guibar
            guitext "Save selection as:"
            guifield newobrname 16
            guicheckbox "Take screenshot" obrscreenshot
            guibutton "^f0Save" [cleargui; savebrush (concatword "brush\"  $obrguidir $newobrname); if ($obrscreenshot) [hidehud 1; sleep 100 [screenshot (concatword "packages\brush\" $obrguidir $newobrname); hidehud 0]]]
        ]
] "obrmodels"

newgui obrmodels [
	guititle "EDITEUR SIMPLIFIÉ"
	guibar
    local i
    local j
    local k
    loop i (div (+ (+ (listlen $obrlist) (listlen $obrfolderlist)) (- 17 1)) 17) [
        if (> $i 0) [
            guitab (+ $i 1)
        ]
        guilist [
            guilist [
                if (! (=s $obrguidir "")) [guibutton "^f2back" [obrguidir = (_prevdir $obrguidir); sleep 1 [obrmodels]] "arrow_bw"]
                loop j (min (- (+ (listlen $obrlist) (listlen $obrfolderlist)) (* $i 17)) 17) [
                    obridx = (+ (* $i 17) $j)
                    if (> $obridx (- (listlen $obrfolderlist) 1)) [
                        guibutton (at $obrlist (- $obridx (listlen $obrfolderlist))) [pastebrush (concatword "brush\" $obrguidir (at $obrlist (- @obridx (listlen $obrfolderlist))))]
                    ] [
                        guibutton (concatword "^f1" (at $obrfolderlist $obridx)) [obrmodels (concatword $obrguidir (at $obrfolderlist @obridx) "\")] "arrow_fw"
                    ]
                ]
                loop k (- 17 (min (- (+ (listlen $obrlist) (listlen $obrfolderlist)) (* $i 17)) 17)) [
                    guitextbox " " 18
                ]
            ]
            guispring
            guibar
            guilist [
                cases (at $guirolloveraction 0) "obrmodels" [guiimage (concatword "packages\brush\" (do (at $guirolloveraction 1)) (do (at (at $guirolloveraction 1) 2)) ".png") $guirolloveraction 5 1] "pastebrush" [guiimage (concatword "packages\" (do (at $guirolloveraction 1)) ".png") $guirolloveraction 5 1]
                guitextbox " " 20  //Maintains window width in the event a brush has no picture
                if ($havesel) [
                    guispring
                    guibar
                    guitext "Sauvegarder sélection :"
                    guifield newobrname 16
                    guicheckbox "Prendre un screenshot" obrscreenshot
                    guibutton "^f0Sauvegarder" [cleargui; savebrush (concatword "brush\" $obrguidir $newobrname); if ($obrscreenshot) [hidehud 1; sleep 100 [screenshot (concatword "packages\brush\" $obrguidir $newobrname); hidehud 0]]]
                ]
            ]
        ]
    ]
] 0